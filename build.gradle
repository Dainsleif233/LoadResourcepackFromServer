plugins {
    id "java"
    id "fabric-loom" version "1.9.2"
    id 'io.papermc.paperweight.userdev' version '2.0.0-beta.8'
    id "com.gradleup.shadow" version "8.3.6"
}

version = project.mod_version
group = project.maven_group
base.archivesName = project.archives_base_name

repositories {
    mavenCentral()
    maven {
        name = "papermc-repo"
        url = "https://repo.papermc.io/repository/maven-public/"
    }
    maven {
        name = "sonatype"
        url = "https://oss.sonatype.org/content/groups/public/"
    }
}

dependencies {
    compileOnly "io.papermc.paper:paper-api:${project.minecraft_version}-R0.1-SNAPSHOT"
    paperweight.paperDevBundle "${project.minecraft_version}-R0.1-SNAPSHOT"
    compileOnly "net.fabricmc:fabric-loader:${project.loader_version}"
    annotationProcessor "org.spongepowered:mixin:0.8.5:processor"
    compileOnly "org.spongepowered:mixin:0.8.5"
    minecraft "com.mojang:minecraft:${project.minecraft_version}"
    mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
    implementation "io.netty:netty-codec-http:4.2.1.Final"
}

processResources {
    filteringCharset "UTF-8"
    inputs.property "mod_version", project.mod_version
    inputs.property "minecraft_version", project.supported_minecraft_versions

    filesMatching("fabric.mod.json") {
        expand "mod_version": project.version,
                "minecraft_version": project.supported_minecraft_versions
    }
    filesMatching("plugin.yml") {
        expand "mod_version": project.version,
                "minecraft_version": project.supported_minecraft_versions
    }
}

def targetJavaVersion = 21
java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = "UTF-8"
}

shadowJar {
    archiveClassifier.set("")
    archiveBaseName.set(base.archivesName)
    minimize()
    dependencies {
        include(dependency("io.netty:netty-codec-http:4.2.1.Final"))
    }
    exclude "mappings/"
    exclude "META-INF/maven/"
    exclude "META-INF/native-image/"
    exclude "META-INF/versions/"
    exclude "META-INF/io.netty.versions.properties"
}